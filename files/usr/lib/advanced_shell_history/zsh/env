#!/bin/bash
#
#   Copyright 2011 Carl Anderson 
#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.
#

source "/usr/lib/advanced_shell_history/common" &>/dev/null || exit 1


setopt extended_history
setopt inc_append_history


##
# Invoked by ash::log in the common library (see parent directory).
#
function ash::last_command() {
  local cmd cmd_no start_ts end_ts=$( date +%s )
  read start_ts <<< "$( grep "^:" "${HISTFILE}" | tail -n1 | cut -d: -f2 )"
  read -r cmd_no cmd <<< "$( builtin history -1 )"
  echo -E ${cmd_no} ${start_ts} ${end_ts} "${cmd}"
}


##
# Placeholder function.
#
function ash::original_precmd() {
  :
}

# Rename the original precmd so it can still be invoked.
source <( typeset -f precmd | sed -e 's/^precmd/ash::original_precmd/' )


##
# Invoked before each new prompt is written, and after the previous command
# has finished.
#
function precmd() {
  ASH_PIPESTATUS=( ${?} ${pipestatus[@]} )
  if [[ -z ${ASH_SESSION_ID} ]]; then
    export ASH_SESSION_ID="$( ash_log --get_session_id )"
    ash_log -a "Advanced Shell History is enabled (session: ${ASH_SESSION_ID})."
    readonly ASH_SESSION_ID
  else
    ash::log ${ASH_PIPESTATUS[@]}
  fi
  ash::original_precmd

  local rval=${ASH_PIPESTATUS[1]}
  ASH_PIPESTATUS=( ${ASH_PIPESTATUS[2,-1]} )
  ash_log --exit ${rval}
}
